grammar io.github.abelgomez.asyncapi.AsyncApi with org.eclipse.xtext.common.Terminals

generate asyncApi "http://io.github.abelgomez/asyncapi/AsyncApi"

AsyncAPI:
	{AsyncAPI} '{'	(
		  ( '"asyncapi"' ':' version=VersionNumber ','? )
		& ( '"info"' ':' info=Info ','? ) 
		& ( '"servers"' ':' '[' servers+=Server (',' servers+=Server)* ']' ','? )? 
		& ( '"topics"' ':' '{' topics+=Topic (',' topics+=Topic)* '}' ','? )
		& ( '"components"' ':' components=Components ','? )?
		& ( '"baseTopic"' ':' baseTopic=STRING ','? )?
	) '}';
		
Info:
	{Info} '{' (
		  ( '"title"' ':' title=STRING ','? ) 
		& ( '"version"' ':' version=STRING ','? )
		& ( '"description"' ':' description=STRING ','? )?
		& ( '"termsOfService"' ':' termsOfService=STRING ','? )?
		& ( '"contact"' ':' contact=Contact ','? )?
		& ( '"license"' ':' license=License ','? )?
	) '}';

Contact:
	{Contact} '{' (
		  ( '"name"' ':' name=STRING ','? )?
		& ( '"url"' ':' url=STRING ','? )?
		& ( '"email"' ':' email=STRING ','? )?
	) '}';

License:
	{License} '{' (
		  ( '"name"' ':' name=STRING ','? )?
		& ( '"url"' ':' url=STRING ','? )?
	) '}';

Server:
	{Server} '{' (
		  ( '"url"' ':' url=STRING ','? )
		& ( '"scheme"' ':' scheme=Scheme ','? )
		& ( '"description"' ':' description=STRING ',' )?
		& ( '"variables"' ':' '{' variables+=Variable (',' variables+=Variable)* '}' ','? )?
	) '}';

Variable:
	{Variable} name=STRING ':' '{' (
		  ( '"description"' ':' description=STRING ','?  )?
		& ( '"default"' ':' default=STRING ','? )?
		& ( '"enum"' ':' '[' ^enum+=STRING (',' ^enum+=STRING)* ']' ','? )?
	) '}';

Topic:
	{Topic} name=STRING ':' '{' (
		  ( '"publish"' ':' publish=AbstractMessage ','?  )?
		& ( '"subscribe"' ':' subscribe=AbstractMessage ','? )?
	) '}';

AbstractMessage:
	Reference | Message;

Message:
	{Message} '{' (
		  ( '"summary"' ':' summary=STRING ','? )?
		& ( '"description"' ':' description=STRING ','? )?
		& ( '"deprecated"' ':' deprecated=Boolean ','? )?
		& ( '"headers"' ':' headers=AbstractSchema ','? )?
		& ( '"tags"' ':' '[' tags+=Tag ( ',' tags+=Tag )* ']' ','? )?
		& ( '"payload"' ':' payload=AbstractSchema ','? )?
	) '}';

NamedMessage:
	{NamedMessage} name=STRING ':' message=AbstractMessage;

Tag:
	{Tag} '{' (
		('"name"' ':' name=STRING ','?)?
		& ('"description"' ':' description=STRING ','?)?
	) '}';

AbstractSchema:
	Reference | Schema;

Schema:
	{Schema} '{' (
		  ( '"title"' ':' title=STRING ','? )?
		& ( '"type"' ':' type=STRING ','? )?
		& ( '"description"' ':' description=STRING ','? )?
		& ( '"format"' ':' format=STRING ','? )?
		& ( '"default"' ':' default=PrimitiveValue','? )?
		& ( '"payload"' ':' payload=AbstractSchema ','? )?
		& ( '"properties"' ':' '{' properties+=NamedSchema (',' properties+=NamedSchema)* '}' ','? )?
		& ( '"enum"' ':' '[' ^enum+=PrimitiveValue (',' ^enum+=PrimitiveValue)* ']' ','? )?
		& ( '"items"' ':' items=AbstractSchema ','? )?
		& ( '"required"' ':' '[' required+=STRING (',' required+=STRING)* ']' ','? )?
		& ( '"x-friendly-name"' ':' friendlyName=STRING ','? )?
	) '}';

NamedSchema:
	{NamedSchema} name=STRING ':' schema=AbstractSchema;

Components:
	{Components} '{' (
		  ( '"schemas"' ':' '{' schemas+=NamedSchema (',' schemas+=NamedSchema)* '}' ','? )?
		& ( '"messages"' ':' '{' messages+=NamedMessage (',' messages+=NamedMessage)* '}' ','? )?
	) '}';


Reference:
	{Reference} (refname=STRING ':')? '{' '"$ref"' ':' uri=STRING '}';

PrimitiveValue:
	STRING | INT | "true" | "false";

enum Boolean:
	_true = "true"
	| _false = "false";

enum VersionNumber:
	_100 = '"1.0.0"'
	| _110 = '"1.1.0"'
	| _120 = '"1.2.0"' ;
	
enum Scheme:
	amqp = '"amqp"'
	| amqps = '"amqps"'
	| mqtt = '"mqtt"'
	| mqtts = '"mqtts"'
	| ws = '"ws"'
	| wss = '"wss"'
	| stomp = '"stomp"'
	| stomps = '"stomps"';
